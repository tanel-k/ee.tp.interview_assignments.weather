plugins {
    id 'org.springframework.boot' version '2.3.0.RELEASE'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'java'
    id 'com.github.node-gradle.node' version '2.2.4'
}

group = 'ee.tp.interview_assignments'
version = '1.0.0'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'io.projectreactor.addons:reactor-extra:3.3.3.RELEASE'

    implementation "javax.xml.bind:jaxb-api"
    implementation "org.glassfish.jaxb:jaxb-runtime"

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
}

node {
    version = '10.16.3'
    download = true
    workDir = file("${project.buildDir}/nodejs")
    npmWorkDir = file("${project.buildDir}/npm")
}

task appNpmInstall(type: NpmTask) {
    workingDir = file("${project.projectDir}/src/frontend")
    args = ["install"]
}

task appNpmBuild(type: NpmTask) {
    dependsOn appNpmInstall
    workingDir = file("${project.projectDir}/src/frontend")
    args = ["run", "build"]
}

task copyWebApp(type: Copy) {
    dependsOn appNpmBuild
    from 'src/frontend/build'
    into 'build/resources/main/static/.'
}

compileJava.dependsOn copyWebApp
